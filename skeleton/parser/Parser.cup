package parser;

import java.util.*;
import java.io.*;
import java_cup.runtime.Symbol;
import java_cup.runtime.ComplexSymbolFactory.ComplexSymbol;
import java_cup.runtime.ComplexSymbolFactory;

import ast.*;
import interpreter.Interpreter;

parser code {: 
  Lexer lexer;

  public Parser(Lexer lex, ComplexSymbolFactory sf) {
    super(lex, sf);
    lexer = lex;
  }

  public void report_fatal_error(String message, Object info) {
    Interpreter.fatalError(message + " (" + info + ")", Interpreter.EXIT_PARSING_ERROR);
  }

  ast.Location loc(int line, int column) {
    return new ast.Location(line, column);
  }

:};

terminal PLUS, LPAREN, RPAREN;
terminal RETURN, MULT, MINUS, SEMI, UMINUS;
terminal Long    INTCONST;

non terminal Program         program;
non terminal Return          return;
non terminal Expr            expr;
non terminal BinaryExpr      binaryExpr;


precedence left PLUS, MINUS;
precedence left MULT;
precedence left UMINUS;
//precedence left ELSE; // resolves dangling else

start with program;

program ::= expr:e
            {: RESULT = new Program(e, loc(eleft, eright)); :}
         |
            return:r
            {: RESULT = new Program(r, loc(rleft, rright)); :}
         ;

return ::= RETURN expr:e SEMI
         {: RESULT = new Return(e, loc(eleft, eright)); :}
      ;

expr ::= INTCONST:c
         {: RESULT = new ConstExpr(c, loc(cleft, cright)); :}
      |  MINUS expr:e 
         {: RESULT = new UnaryExpr(e, UnaryExpr.UMINUS, loc(eleft, eright)); :}
         %prec UMINUS
      |  binaryExpr:e
         {: RESULT = e; :}
      |  LPAREN expr:e RPAREN
         {: RESULT = e; :}
      ;

binaryExpr ::= expr:e1 PLUS expr:e2
               {: RESULT = new BinaryExpr(e1, BinaryExpr.PLUS, e2, loc(e1left, e1right)); :}
            |  expr:e1 MINUS expr:e2
               {: RESULT = new BinaryExpr(e1, BinaryExpr.BMINUS, e2, loc(e1left, e1right)); :}
            |  expr:e1 MULT expr:e2
               {: RESULT = new BinaryExpr(e1, BinaryExpr.MULT, e2, loc(e1left, e1right)); :}
            ;
         
